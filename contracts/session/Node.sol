// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.0;

import "./interfaces/INode.sol";
import "../libraries/WireLibrary.sol";

abstract contract Node is INode {
    NodeType thisNode;

    address public prevNode;
    address public nextNode;

    Nodes nodes;

    mapping(address => Pair) public pairs;
    mapping(address => mapping(address => address)) public pairFor;

    FeeStores public feeStores;
    mapping(ActionType => FeeRates) public feeRates;

    function getOwner() public virtual returns (address);

    modifier wired() {
        require(msg.sender == prevNode || msg.sender == address(this) || msg.sender == getOwner(), "Invalid caller 1");
        _;
    }

    modifier internalCall() virtual {
        require(WireLibrary.isWiredCall(nodes), "Invalid caller 2");
        _;
    }

    constructor(NodeType _nodeType) {
        thisNode = _nodeType;
    }

    function wire(address _prevNode, address _nextNode) external virtual override {
        require(msg.sender == getOwner(), "Invalid caller 3");
        prevNode = _prevNode;
        nextNode = _nextNode;
    }

    function setNode(
        NodeType nodeType,
        address node,
        address caller
    ) public virtual override wired {
        if (caller != address(this)) {
            // let caller be address(0) when an actor initiats this loop.
            WireLibrary.setNode(nodeType, node, nodes);
            address trueCaller = caller == address(0) ? address(this) : caller;
            INode(nextNode).setNode(nodeType, node, trueCaller);
        } else {
            emit SetNode(nodeType, node);
        }
    }

    bool internal trackPairStatus;

    function changePairStatus(
        address pair,
        address token0,
        address token1,
        ListStatus status,
        address caller
    ) public virtual override wired {
        if (caller != address(this)) {
            // let caller be address(0) when an actor initiats this loop.
            if (trackPairStatus) {
                pairs[pair] = Pair(token0, token1, status);
                pairFor[token0][token1] = pair;
                pairFor[token1][token0] = pair;
            }
            address trueCaller = caller == address(0) ? address(this) : caller;
            INode(nextNode).changePairStatus(pair, token0, token1, status, trueCaller);
        } else {
            emit ChangePairStatus(pair, token0, token1, status);
        }
    }

    function _checkEnlisted(address pair) internal view {
        require(pairs[pair].status == ListStatus.Enlisted, "Pair not enlisted");
    }

    bool internal trackFeeStores;

    function setFeeStores(FeeStores memory _feeStores, address caller) public virtual override wired {
        if (caller != address(this)) {
            // let caller be address(0) when an actor initiats this loop.
            if (trackFeeStores) WireLibrary.setFeeStores(feeStores, _feeStores);
            address trueCaller = caller == address(0) ? address(this) : caller;
            INode(nextNode).setFeeStores(_feeStores, trueCaller);
        } else {
            emit SetFeeStores(_feeStores);
        }
    }

    bool internal trackFeeRates;

    function setFeeRates(
        ActionType _sessionType,
        FeeRates memory _feeRates,
        address caller
    ) public virtual override wired {
        if (caller != address(this)) {
            // let caller be address(0) when an actor initiats this loop.
            if (trackFeeRates) WireLibrary.setFeeRates(_sessionType, feeRates, _feeRates);
            address trueCaller = caller == address(0) ? address(this) : caller;
            INode(nextNode).setFeeRates(_sessionType, _feeRates, trueCaller);
        } else {
            emit SetFeeRates(_sessionType, _feeRates);
        }
    }

    function begin(address caller) public virtual override wired {
        if (caller != address(this)) {
            // let caller be address(0) when an actor initiats this loop.
            INode(nextNode).begin(caller == address(0) ? address(this) : caller);
        } else {
            emit Begin();
        }
    }
}
